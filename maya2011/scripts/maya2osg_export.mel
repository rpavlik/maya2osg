//
//     Maya2OSG - A toolkit for exporting Maya scenes to OpenSceneGraph
//     Copyright (C) 2010 Javier Taibo <javier.taibo@gmail.com>
//     http://sourceforge.net/projects/maya2osg/
//
//     This file is part of Maya2OSG.
//
//     Maya2OSG is free software: you can redistribute it and/or modify
//     it under the terms of the GNU General Public License as published by
//     the Free Software Foundation, either version 3 of the License, or
//     (at your option) any later version.
//
//     Maya2OSG is distributed in the hope that it will be useful,
//     but WITHOUT ANY WARRANTY; without even the implied warranty of
//     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//     GNU General Public License for more details.
//
//     You should have received a copy of the GNU General Public License
//     along with Maya2OSG.  If not, see <http://www.gnu.org/licenses/>.
//


global proc maya2osg_doExport(string $file, string $format)
{
	$command = "maya2osg";

	// If scene is Z-Up do nothing, if scene is Y-Up repeat last behaviour
	if ( `upAxis -q -axis` == "z" ) {
		$command += " -YUp2ZUp 0";
	}
	$command += " \"" + $file + "\";";
	eval($command);
}

global proc maya2osg_doExportGUIAndPreview(string $file, string $format)
{
	maya2osg_doExportGUI($file, $format);
	exec("osgviewer --window 100 100 640 480 " + $file);
}

global proc maya2osg_doExportGUI(string $file, string $format)
{
	global string $maya2osg_export_win;
	global string $maya2osg_texenv;
	global string $maya2osg_cbDefaultCameras;
	global string $maya2osg_cbOrthoCameras;
	global string $maya2osg_bfcull;
	global string $maya2osg_texclamp;
	global string $maya2osg_yup2zup;
	global string $maya2osg_export_anim;
	global string $maya2osg_anim_sampleby;
	global string $maya2osg_cbParticlesEmissive;
	global string $maya2osg_cbParticlesLighting;
	global string $maya2osg_compute_shadows;
	global string $maya2osg_shadow_technique;
	global string $maya2osg_global_ambient;
	global string $maya2osg_local_viewer;
	global string $maya2osg_use_glsl;

	string $command = "maya2osg";
		
	string $texenv[];
	$texenv = `textScrollList -q -selectItem $maya2osg_texenv`;
	$command += " -texenv " + $texenv[0];

	string $texclamp[];
	$texclamp = `textScrollList -q -selectItem $maya2osg_texclamp`;
	$command += " -texClampMode " + $texclamp[0];

	$command += " -exportDefaultCameras " + `checkBox -q -value $maya2osg_cbDefaultCameras`;
	$command += " -exportOrthoCameras " + `checkBox -q -value $maya2osg_cbOrthoCameras`;

	int $bfcull_mode = `radioButtonGrp -q -select $maya2osg_bfcull`;
	switch ( $bfcull_mode ) {
		case 1: // All single
		$command += " -surfaceMode SINGLE";
		break;
		case 2: // All double
		$command += " -surfaceMode DOUBLE";
		break;
		case 3: // Keep
		$command += " -surfaceMode KEEP";
		break;
	}

	// Y-Up 2 Z-Up
	if ( `upAxis -q -axis` == "y" ) {
		$command += " -YUp2ZUp " + `checkBox -q -value $maya2osg_yup2zup`;
	}
	else {
		$command += " -YUp2ZUp 0";
	}
	
	// Export animations
	$command += "-exportAnim " + `checkBox -q -value $maya2osg_export_anim`;
	
	// Animation sample by
	$command += "-animSampleBy " + `intField -q -value $maya2osg_anim_sampleby`;

	// Particle systems
	$command += " -particlesEmissive " + `checkBox -q -value $maya2osg_cbParticlesEmissive`;
	$command += " -particlesLighting " + `checkBox -q -value $maya2osg_cbParticlesLighting`;

	// Shadows computation
	$command += "-computeShadows " + `checkBox -q -value $maya2osg_compute_shadows`;

	string $shadow_technique[];
	$shadow_technique = `textScrollList -q -selectItem $maya2osg_shadow_technique`;
	$command += " -shadowTechnique " + $shadow_technique[0];

	// Lighting model
	float $ambient_color[] = `colorSliderGrp -q -rgbValue $maya2osg_global_ambient`;
	$command += " -globalAmbient " + $ambient_color[0] + " " + $ambient_color[1] + " " + $ambient_color[2];
	$command += " -localViewer " + `checkBox -q -value $maya2osg_local_viewer`;

	// GLSL shaders
	$command += " -useGLSL " + `checkBox -q -value $maya2osg_use_glsl`;
		
	$command += " \"" + $file + "\";";
//	print($command + "\n");
	eval($command);

	deleteUI -window $maya2osg_export_win;
}


proc maya2osg_options(){

	global string $maya2osg_export_win;
	global string $maya2osg_texenv;
	global string $maya2osg_cbDefaultCameras;
	global string $maya2osg_cbOrthoCameras;
	global string $maya2osg_bfcull;
	global string $maya2osg_texclamp;
	global string $maya2osg_yup2zup;
	global string $maya2osg_export_anim;
	global string $maya2osg_anim_sampleby;
	global string $maya2osg_cbParticlesEmissive;
	global string $maya2osg_cbParticlesLighting;
	global string $maya2osg_compute_shadows;
	global string $maya2osg_shadow_technique;
	global string $maya2osg_global_ambient;
	global string $maya2osg_local_viewer;
	global string $maya2osg_use_glsl;

	// Open a dialog for the selection of export options
	int $marginW = 15;
	int $marginH = 10;
	int $line_height = 25;
	$maya2osg_export_win = `window -title "Maya2OSG - Export options" -widthHeight 320 300 -sizeable true`;
	$form = `formLayout -numberOfDivisions 100`;
		$frame_config = `scrollLayout -horizontalScrollBarThickness 0 -verticalScrollBarThickness 0 -childResizable true -minChildWidth 300`;
			columnLayout -adjustableColumn true;
				$frame_geometry = `frameLayout -label "Geometry" -collapsable true -marginWidth $marginW -marginHeight $marginH`;
					columnLayout;
						text -label "Single/double side surfaces:" -height $line_height;
						$maya2osg_bfcull = `radioButtonGrp -numberOfRadioButtons 3 -select 3 -label1 "all single" -label2 "all double" -label3 "keep state"`;
						if ( `upAxis -q -axis` == "y" ) {
							$maya2osg_yup2zup = `checkBox -label "Convert from Y-Up to Z-Up" -value true -height $line_height`;
						}
						setParent ..;
					setParent ..;
				$frame_textures = `frameLayout -label "Texturing" -collapsable true -marginWidth $marginW -marginHeight $marginH`;
					rowLayout -nc 2;
// 						rowLayout -nc 2;
// 							text -label "Image path" -enable false;
// 							textField -editable true -width 300 -enable false;
// 							setParent ..;
						columnLayout;
							text -label "Default mode: ";
							$maya2osg_texenv = `textScrollList -numberOfRows 3
								-allowMultiSelection false
								-append "DECAL"
								-append "MODULATE"
								-append "BLEND"
								-append "REPLACE"
								-append "ADD"
								-selectItem "MODULATE"`;
							setParent ..;
						columnLayout;
							text -label "Clamping: ";
							$maya2osg_texclamp = `textScrollList -numberOfRows 2
								-allowMultiSelection false
								-append "EDGE"
								-append "COLOR"
								-selectItem "COLOR"`;
							setParent ..;
						setParent ..;
					setParent ..;
				$frame_geometry = `frameLayout -label "Materials/shading" -collapsable true -marginWidth $marginW -marginHeight $marginH`;
					columnLayout;
						//text -label "Coming soon..." -height $line_height;
						$maya2osg_use_glsl = `checkBox -label "Build GLSL shaders" -value 0 -height $line_height`;
						setParent ..;
					setParent ..;
				$frame_geometry = `frameLayout -label "Lighting" -collapsable true -marginWidth $marginW -marginHeight $marginH`;
					columnLayout;
						$maya2osg_global_ambient = `colorSliderGrp -label "Global ambient color" -rgbValue 0 0 0`;
						$maya2osg_local_viewer = `checkBox -label "Local viewer" -value 1 -height $line_height`;
						$maya2osg_compute_shadows = `checkBox -label "Compute shadows" -value 1 -height $line_height`;
						text -label "Shadow technique: ";
						$maya2osg_shadow_technique = `textScrollList -numberOfRows 3
							-allowMultiSelection false
							-append "SHADOW_MAP"
							-append "SOFT_SHADOW_MAP"
							-append "SHADOW_VOLUME"
							-append "SHADOW_TEXTURE"
							-append "PARALLEL_SPLIT_SHADOW_MAP"
							-selectItem "SHADOW_MAP"`;
						setParent ..;
					setParent ..;
				$frame_geometry = `frameLayout -label "Animation" -collapsable true -marginWidth $marginW -marginHeight $marginH`;
					columnLayout;
						$maya2osg_export_anim = `checkBox -label "Export animations" -value 1 -height $line_height`;
						rowLayout -nc 2;
							text -label "Sample by: ";
							$maya2osg_anim_sampleby = `intField -minValue 1 -maxValue 100 -step 1 -value 1`;
							setParent ..;
						setParent ..;
					setParent ..;
				$frame_geometry = `frameLayout -label "Particles" -collapsable true -marginWidth $marginW -marginHeight $marginH`;
					columnLayout;
						$maya2osg_cbParticlesEmissive = `checkBox -label "Emissive particles"`;
						$maya2osg_cbParticlesLighting = `checkBox -label "Particles lighting"`;
						setParent ..;
					setParent ..;
				$frame_cameras = `frameLayout -label "Cameras" -collapsable true -marginWidth $marginW -marginHeight $marginH`;
					columnLayout;
						$maya2osg_cbDefaultCameras = `checkBox -label "Export default cameras"`;
						$maya2osg_cbOrthoCameras = `checkBox -label "Export orthographic cameras"`;
						setParent ..;
					setParent ..;
				setParent ..;
			setParent ..;
		$buttons = `formLayout  -numberOfDivisions 100`;
			$comm_export = "fileBrowserDialog -an \"Export as...\" -fc \"maya2osg_doExportGUI\" -ft \"osg\" -in \"OSG\" -m 1 -om \"ExportAll\";";
			$comm_export_and_preview = "fileBrowserDialog -an \"Export as...\" -fc \"maya2osg_doExportGUIAndPreview\" -ft \"osg\" -in \"OSG\" -m 1 -om \"ExportAll\";";
			$b1 = `button -label "Export" -align "center" -command $comm_export`;
			$b2 = `button -label "Export and Preview" -align "center" -command $comm_export_and_preview`;
			$b3 = `button -label "Cancel" -align "center" -command "deleteUI -window $maya2osg_export_win"`;
			formLayout -e
				-attachForm $b1 "top" 10
				-attachForm $b1 "left" 10
				-attachControl $b1 "right" 6 $b2
				-attachForm $b1 "bottom" 10

				-attachForm $b2 "top" 10
				-attachPosition $b2 "left" 6 33
				-attachControl $b2 "right" 6 $b3
				-attachForm $b2 "bottom" 10
				
				-attachForm $b3 "top" 10
				-attachPosition $b3 "left" 6 66
				-attachForm $b3 "right" 10
				-attachForm $b3 "bottom" 10
				$buttons;
			setParent ..;
		formLayout -e
			-attachForm $frame_config "top" 0
			-attachForm $frame_config "left" 0
			-attachForm $frame_config "right" 0
			-attachControl $frame_config "bottom" 0 $buttons
			-attachNone $buttons "top"
			-attachForm $buttons "left" 0
			-attachForm $buttons "right" 0
			-attachForm $buttons "bottom" 0
			$form;
		setParent ..;

	showWindow $maya2osg_export_win;
}


global proc maya2osg_export( int $option_box ) {

	loadPlugin -quiet "maya2osg";
	// check if it successfully loaded
	if ( ! `pluginInfo -q -loaded maya2osg` ) {
//		error "Maya2OSG plug-in failed to load";
		return;
	}

	// Cleanup has been deactivated. It can be explicitly called
	// by the user from the shelf button "Cleanup scene".
//	maya2osg_cleanup();

	if ( $option_box ) {
		// Open the export options dialog
		maya2osg_options();
	}
	else {
		fileBrowserDialog -an "Export as..." -fc "maya2osg_doExport" -ft "osg"
			-in "OSG" -m 1 -om "ExportAll";
	}
}
